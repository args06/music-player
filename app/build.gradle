plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id "androidx.navigation.safeargs"
}

android {
    namespace 'com.example.nuberjam'
    compileSdk 33

    defaultConfig {
        applicationId "com.example.nuberjam"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        Properties properties = new Properties()
        properties.load(project.rootProject.file("local.properties").newDataInputStream())
        pre {
            versionNameSuffix ".pre"
            applicationIdSuffix ".pre"
            minifyEnabled false
            shrinkResources false
            debuggable true
            buildConfigField "String", "BASE_API_URL", '"http://nuberjam-dev.orgfree.com/api/"'
            buildConfigField "String", "TOKEN", "${properties.getProperty("PRE_TOKEN")}"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        prod {
            minifyEnabled true
            shrinkResources true
            buildConfigField "String", "BASE_API_URL", '"http://nuberjam.orgfree.com/api/"'
            buildConfigField "String", "TOKEN", "${properties.getProperty("PROD_TOKEN")}"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

ext {
    // Core
    coreVersion = "1.10.1"
    appcompatVersion = "1.6.1"
    lifecycleVersion = "2.6.1"
    activityVersion = "1.7.2"
    fragmentVersion = "1.6.0"

    // UI
    recyclerviewVersion = "1.3.0"
    materialVersion = "1.9.0"
    constraintlayoutVersion = "2.1.4"

    // Navigation
    navigationVersion = "2.6.0"

    // Photo Online Loader
    glideVersion = "4.15.1"

    // API
    retrofit2Version = "2.9.0"
    okhttp3Version = "4.9.0"

    // Local Database
    roomVersion = "2.5.2"

    // Preferences DataStore
    datastoreVersion = "1.0.0"

    // Unit Testing
    junitVersion = "4.13.2"
    mockitoVersion = "4.4.0"

    // Integration Testing
    runnerVersion = "1.5.2"
    espressoVersion = "3.5.1"
    rulesVersion = "1.5.0"
    extVersion = "1.1.5"
}

dependencies {
    // Core
    implementation "androidx.core:core-ktx:$coreVersion"
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.activity:activity-ktx:$activityVersion"
    implementation "androidx.fragment:fragment-ktx:$fragmentVersion"

    // UI
    implementation "androidx.recyclerview:recyclerview:$recyclerviewVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayoutVersion"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    // Photo Online Loader
    implementation "com.github.bumptech.glide:glide:$glideVersion"

    // API
    implementation "com.squareup.retrofit2:retrofit:$retrofit2Version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2Version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3Version"

    // Local Database
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"

    // Preferences DataStore
    implementation "androidx.datastore:datastore-preferences:$datastoreVersion"

    // Unit Testing
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-inline:$mockitoVersion"

    // Integration Testing
    androidTestImplementation "androidx.test:runner:$runnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test:rules:$rulesVersion"
    androidTestImplementation "androidx.test.ext:junit:$extVersion"
}